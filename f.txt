Program -> Declarationlist
Declarationlist -> Declaration Declarationlist
Declarationlist -> ε
Declaration -> Declarationinitial Declarationprime
Declarationinitial -> Typespecifier ID
Declarationprime -> Fundeclarationprime 
Declarationprime -> Vardeclarationprime
Vardeclarationprime -> ;
Vardeclarationprime -> [ NUM ] ;
Fundeclarationprime -> ( Params ) Compoundstmt
Typespecifier -> int
Typespecifier -> void
Params -> int ID Paramprime Paramlist
Params -> void
Paramlist -> , Param Paramlist
Paramlist ->  ε
Param -> Declarationinitial Paramprime
Paramprime -> [ ]
Paramprime -> ε
Compoundstmt -> { Declarationlist Statementlist }
Statementlist -> Statement Statementlist
Statementlist -> ε
Statement -> Expressionstmt 
Statement -> Compoundstmt
Statement -> Selectionstmt
Statement -> Iterationstmt
Statement -> Returnstmt
Expressionstmt -> Expression ;
Expressionstmt -> break ;
Expressionstmt -> ;
Selectionstmt -> if ( Expression ) Statement Elsestmt
Elsestmt -> endif
Elsestmt -> else Statement endif
Iterationstmt -> repeat Statement until ( Expression )
Returnstmt -> return Returnstmtprime
Returnstmtprime -> ; 
Returnstmtprime -> Expression ;
Expression -> Simpleexpressionzegond
Expression -> ID B
B -> = Expression
B -> [ Expression ] H
B -> Simpleexpressionprime
H -> = Expression 
H ->  G D C
Simpleexpressionzegond -> Additiveexpressionzegond C
Simpleexpressionprime -> Additiveexpressionprime C
C -> Relop Additiveexpression
C -> ε
Relop -> < 
Relop ->  ==
Additiveexpression -> Term D
Additiveexpressionprime -> Termprime D
Additiveexpressionzegond -> Termzegond D
D -> Addop Term D
D -> ε
Addop -> +
Addop -> -
Term -> Factor G
Termprime -> Factorprime G
Termzegond -> Factorzegond G
G -> * Factor G
G -> ε
Factor -> ( Expression )
Factor -> ID Varcallprime
Factor -> NUM
Varcallprime -> ( Args )
Varcallprime -> Varprime
Varprime -> [ Expression ]
Varprime -> ε
Factorprime -> ( Args )
Factorprime -> ε
Factorzegond -> ( Expression )
Factorzegond -> NUM
Args -> Arglist
Args -> ε
Arglist -> Expression Arglistprime
Arglistprime -> , Expression Arglistprime
Arglistprime -> ε

